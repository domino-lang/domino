package PRF {
    params {
        n: Integer,
      prf: fn Bits(n), (Integer, Integer, Bits(n), Bits(n), Bool) ->  Bits(n),
        b: Bool
    }
    
    state {
          LTK:   Table(Integer, Bits(n)),       /* administrative kid, keys    */
          H:     Table(Integer, Bool),          /* administrative kid, honesty */
          PRF:   Table((Integer,(Integer,Integer,Bits(n),Bits(n),Bool)), Bits(n)), /* output keys */
          kid_:  Integer,                       /* counter */
    }

oracle NewKey(ltk: Maybe(Bits(n))) -> Integer {
      kid_ <- (kid_ + 1);
      if (ltk == None)
      {
        ltk_ <-$ Bits(n);
        LTK[kid_] <- Some(ltk_);
        H[kid_]   <- Some(true); 
      }
      else {
        LTK[kid_] <- ltk;
        H[kid_]   <- Some(false);}
      return kid_;
    }

    oracle Eval(kid: Integer, x: (Integer, Integer, Bits(n), Bits(n), Bool)) -> Bits(n) {
        assert not (LTK[kid] == None);
        if ((H[kid] == Some(false)) or (b == false))
            {
              k <- Unwrap(LTK[kid]);
              return prf(k,x);
            }
        if (PRF[(kid,x)] == None) {
            temp <-$ Bits(n);
            PRF[(kid,x)] <- Some(temp);
        }
        y <- PRF[(kid,x)];
        return Unwrap(y);
    }
    oracle Hon(kid: Integer) -> Bool {
        assert not (H[kid] == None);
		return Unwrap(H[kid]);
    }
}