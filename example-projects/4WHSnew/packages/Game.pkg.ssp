package Game {
    params {
        b: Bool,
        n: Integer,
        zeron: Bits(n),
    }
    
    state {
          LTK:   Table(Integer, Bits(n)),       /* administrative kid, keys    */
          H:     Table(Integer, Bool),          /* administrative kid, honesty */
          ctr_:  Integer,                       /* administrative sid  */
          kid_:  Integer,                       /* administrative kid  */
		  /* session key table, reveal -> (.,false) ; test -> (.,true) */
          Keys:  Table((Integer,Integer,Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n))),
		               (Bits(n),Bool)), 
          State: Table(Integer,
                  /*    administrative sid  */
                (
                 Integer,
                  /*    ID of owner of the session */
                 Bool,
                  /*    false: initiator, 
                        true : responder,*/ 
                 Integer,
                  /* Intended Peer */
                 Bool,
                  /*    false: initiator, 
                        true : responder,*/ 
                 Bits(n),
                  /*    ltk          */
                 Maybe(Bool),
                  /* None: running, 
                        true: accepted, 
                        false: rejected   */
                 Maybe(Bits(n)),
                        /* session key */
                 Maybe(Bits(n)),
                        /* initiator nonce */
                 Maybe(Bits(n)),
                        /* responder nonce */
                 Maybe(Bits(n)),
                        /* MAC key */
                 Maybe((Integer,Integer,Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)))),
                        /* SID */
                 Integer))
                        /* nbr of messages sent */

          }

    import oracles {
         Run1(state: (Integer,Bool,Integer,Bool,Bits(n),Maybe(Bool),
                        Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)),
                        Maybe((Integer,Integer,Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)))),
                        Integer))                            
         -> ((Integer,Bool,Integer,Bool,Bits(n),Maybe(Bool),
                        Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)),
                        Maybe((Integer,Integer,Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)))),
                        Integer),
            Bits(n)), 
         Run2(state: (Integer,Bool,Integer,Bool,Bits(n),Maybe(Bool),
                        Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)),
                        Maybe((Integer,Integer,Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)))),
                        Integer), ni: Bits(n))               
         -> ((Integer,Bool,Integer,Bool,Bits(n),Maybe(Bool),
                        Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)),
                        Maybe((Integer,Integer,Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)))),
                        Integer),
            (Bits(n),Bits(n))), 
         Run3(state: (Integer,Bool,Integer,Bool,Bits(n),Maybe(Bool),
                        Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)),
                        Maybe((Integer,Integer,Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)))),
                        Integer), nr: Bits(n), tau: Bits(n)) 
         -> ((Integer,Bool,Integer,Bool,Bits(n),Maybe(Bool),
                        Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)),
                        Maybe((Integer,Integer,Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)))),
                        Integer),
            (Bits(n),Bits(n))), 
         Run4(state: (Integer,Bool,Integer,Bool,Bits(n),Maybe(Bool),
                        Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)),
                        Maybe((Integer,Integer,Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)))),
                        Integer), ni: Bits(n), tau: Bits(n)) 
         -> ((Integer,Bool,Integer,Bool,Bits(n),Maybe(Bool),
                        Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)),
                        Maybe((Integer,Integer,Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)))),
                        Integer),
            Bits(n)), 
         Run5(state: (Integer,Bool,Integer,Bool,Bits(n),Maybe(Bool),
                        Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)),
                        Maybe((Integer,Integer,Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)))),
                        Integer), tau: Bits(n))              
         -> ((Integer,Bool,Integer,Bool,Bits(n),Maybe(Bool),
                        Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)),
                        Maybe((Integer,Integer,Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)))),
                        Integer),
            Bool),
    }



oracle NewKey(ltk: Maybe(Bits(n))) -> Integer {
      kid_ <- (kid_ + 1);
      if (ltk == None)
      {
        ltk_ <-$Bits(n);
        LTK[kid_] <- Some(ltk_);
        H[kid_]   <- Some(true); 
      }
      else {
        LTK[kid_] <- ltk;
        H[kid_]   <- Some(false);}
      return kid_;
    }


oracle NewSession(U: Integer, u: Bool, V: Integer, v: Bool, kid: Integer) -> Integer {
        assert not (u == v);            /* Different roles */
        assert not (LTK[kid] == None);  /* key exists */
        ctr_        <- (ctr_ + 1) ; 
        ltk         <- Unwrap(LTK[kid]);
        State[ctr_] <- Some((U,u,V,v,ltk,None,None,None,None,None,None,0));
        return ctr_;
    }

oracle Send1(ctr: Integer) -> Bits(n)
    {
        assert not (State[ctr] == None);     /* session has been initialized */
        state <- Unwrap(State[ctr]);
        (U,u,V,v,ltk,acc,k,ni,nr,kmac,sid,mess) <- parse state;
        assert (u == false);                 /* session belongs to initiator */
        assert (mess == 0);                  /* session has not yet sent its first message */
        assert (acc  == None );              /* session is still running           */
        return      <- invoke Run1(state);
        (state,msg) <- parse return;
        State[ctr]  <- Some(state);
        return msg;
    } 

oracle Send2(ctr: Integer, msg: Bits(n)) -> (Bits(n),Bits(n))
    {
        assert not (State[ctr] == None);     /* session has been initialized */
        state   <- Unwrap(State[ctr]);
        (V,v,U,u,ltk,acc,k,ni_,nr,kmac,sid,mess) <- parse state;
        assert (v    == true );              /* session belongs to responder */
        assert (mess == 0    );              /* session has not yet sent its first message */
        assert (acc  == None );              /* session is still running           */
        return         <- invoke Run2(state,msg);
        (state,msg_)   <- parse return;
        State[ctr]     <-  Some(state);
        return msg_;
    } 

oracle Send3(ctr: Integer, msg: (Bits(n),Bits(n))) -> (Bits(n), Bits(n))
    {
        assert not (State[ctr] == None);    /* session has been initialized */
        state                                           <- Unwrap(State[ctr]);
        (U,u,V,v,ltk,acc,k_,ni_,nr_,kmac_,sid_,mess)    <- parse state;
        assert (u    == false);              /* session belongs to initiator       */
        assert (mess == 1);                  /* session has sent its first message */
        assert (acc  == None);               /* session is still running           */
        return         <- invoke Run3(state,msg);
        (state,msg_)   <- parse return;
        State[ctr]     <-  Some(state);
        return msg_;
    } 

oracle Send4(ctr: Integer, msg: (Bits(n),Bits(n))) -> Bits(n)
    {
        assert not (State[ctr] == None);     /* session has been initialized */
        state                                          <- Unwrap(State[ctr]);
        (V,v,U,u,ltk,acc,k,ni_,nr,kmac,sid_,mess)      <- parse state;
		
        assert (v    == true);                /* session belongs to responder       */
        assert (mess == 1);                   /* session has sent its first message */
        assert (acc  == None);                /* session is still running           */

        return       <- invoke Run4(state,msg);
        (state,msg_) <- parse return;
        State[ctr]   <- Some(state);

        (V,v,U,u,ltk,acc,k,ni_,nr,kmac,sid,mess) <- parse state;
        return msg_;  
    } 

        oracle Send5(ctr: Integer, msg: Bits(n)) -> Bool
    {
        assert not (State[ctr] == None);    /* session has been initialized */
        state                                       <- Unwrap(State[ctr]);
        (U,u,V,v,ltk,acc,k,ni,nr,kmac,sid,mess)     <- parse state;
		
        assert (u    == false);              /* session belongs to initiator       */
        assert (mess == 2);                  /* session has sent its 2nd message   */
        assert (acc  == None);               /* session is still running           */
		
        return      <- invoke Run5(state,msg);
        (state,stop) <- parse return;
        if (stop == false) {return false;}   /* state is not updated, adversary can try again */
        State[ctr] <- Some(state);
        (U,u,V,v,ltk,acc,k,ni,nr,kmac,sid,mess)     <- parse state;

        return stop;
    } 

oracle Reveal(ctr: Integer) -> Bits(n)
    {
    (U,u,V,v,ltk,acc,k,ni,nr,kmac,sid,mess) <- parse Unwrap(State[ctr]);

	/* Session accepted */
    assert (acc == Some(true));

	/* only allowed to test/reveal sid once */
    assert (Keys[Unwrap(sid)] == None);

	/* mark key as revealed */
    Keys[Unwrap(sid)] <- Some((Unwrap(k),false));    
    return Unwrap(k);
    }

oracle Test(ctr: Integer) -> Bits(n)
    {
    (U,u,V,v,ltk,acc,k,ni,nr,kmac,sid,mess) <- parse Unwrap(State[ctr]);
	
	/* Session accepted */
    assert (acc == Some(true));

	/* only allowed to test/reveal sid once */
    assert (Keys[Unwrap(sid)] == None);

	/* mark key as tested */
    Keys[Unwrap(sid)] <- Some((Unwrap(k),true));
	
    k_ <- Unwrap(k);
    if b {
	    k_ <-$ Bits(n);
	}
    return k_;
    }

    }