package Prot {
    params {
        n: Integer,
        zeron: Bits(n),
        prf: fn Bits(n), (Integer, Integer, Bits(n), Bits(n), Bool) ->  Bits(n),
        mac: fn Bits(n), Bits(n), Integer -> Bits(n),
    }

	oracle Run1(state: (Integer,Bool,Integer,Bits(n),Maybe(Bool),
	                    Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)),
						Maybe((Integer,Integer,Bits(n),Bits(n),Bits(n))),
						Integer)) -> ((Integer,Bool,Integer,Bits(n),Maybe(Bool),
				                       Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)),
						               Maybe((Integer,Integer,Bits(n),Bits(n),Bits(n))),
						               Integer),
						              (Bits(n))) {
	    (U,u,V,ltk,acc,k,ni_,nr,kmac,sid,mess) <- parse state;
        assert (acc  == None );              /* session is still running           */
        assert (mess == 0);
        assert (u == false);                 /* session belongs to initiator */
		
		ni <-$ Bits(n);
		
		return ((U,u,V,ltk,None,k,Some(ni),nr,kmac,sid,1),
		        (ni));
	}

	oracle Run2(state: (Integer,Bool,Integer,Bits(n),Maybe(Bool),
	                    Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)),
						Maybe((Integer,Integer,Bits(n),Bits(n),Bits(n))),
						Integer),
	            ni:Bits(n)) -> ((Integer,Bool,Integer,Bits(n),Maybe(Bool),
				                    Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)),
							        Maybe((Integer,Integer,Bits(n),Bits(n),Bits(n))),
							        Integer),
							       (Bits(n), Bits(n))) {
	    (U,v,V,ltk,acc,k_,ni_,nr_,kmac_,sid_,mess) <- parse state;
        assert (acc  == None );              /* session is still running           */
        assert (mess == 0);
        assert (v    == true );              /* session belongs to responder */

		nr         <-$ Bits(n);
        k          <-  prf(ltk,(U,V,ni,nr,true)); 
        kmac       <-  prf(ltk,(U,V,ni,nr,false));
        tau        <-  mac(kmac,nr,2);
        sid        <- (U,V,ni,nr,tau);

		return ((U,v,V,ltk,None,Some(k),Some(ni),Some(nr),Some(kmac),Some(sid),1),
		        (nr,tau));
	}

	oracle Run3(state: (Integer,Bool,Integer,Bits(n),Maybe(Bool),
	                    Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)),
						Maybe((Integer,Integer,Bits(n),Bits(n),Bits(n))),
						Integer),
	            msg:(Bits(n),Bits(n))) -> ((Integer,Bool,Integer,Bits(n),Maybe(Bool),
				                            Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)),
							                Maybe((Integer,Integer,Bits(n),Bits(n),Bits(n))),
							                Integer),
							               (Bits(n), Bits(n))) {
	    (U,u,V,ltk,acc,k_,ni,nr_,kmac_,sid_,mess) <- parse state;
        assert (acc  == None );              /* session is still running           */
        assert (mess == 1);
        assert (u    == false);              /* session belongs to initiator       */

        (nr,tau) <- parse msg;

        k          <- prf(ltk,(U,V,Unwrap(ni),nr,true));
		kmac       <- prf(ltk,(U,V,Unwrap(ni),nr,false));
        tau_       <- mac(kmac,Unwrap(ni),3);
        sid        <- (U,V,Unwrap(ni),nr,tau);

        if (mac(kmac,nr,2) == tau) {
		   return ((U,u,V,ltk,None,Some(k),ni,Some(nr),Some(kmac),Some(sid),2),
		           (Unwrap(ni),tau_));
		} else {
		   return (state, (zeron,zeron));
		}
	}

	oracle Run4(state: (Integer,Bool,Integer,Bits(n),Maybe(Bool),
	                    Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)),
						Maybe((Integer,Integer,Bits(n),Bits(n),Bits(n))),
						Integer),
	            msg:(Bits(n),Bits(n))) -> ((Integer,Bool,Integer,Bits(n),Maybe(Bool),
				                            Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)),
							                Maybe((Integer,Integer,Bits(n),Bits(n),Bits(n))),
							                Integer),
							               (Bits(n))) {
	    (U,v,V,ltk,acc,k,ni_,nr,kmac,sid,mess) <- parse state;
        assert (acc  == None );              /* session is still running           */
		assert (mess == 1);
        assert (v    == true);                /* session belongs to responder       */

        (ni,tau) <- parse msg;

        if ((mac(Unwrap(kmac),ni,3) == tau) and
            (ni == Unwrap(ni_))) {
		    acc        <- Some(true);
			tau_       <- mac(Unwrap(kmac),zeron,4);
			
		   return ((U,v,V,ltk,acc,k,ni_,nr,kmac,sid,2),
		           (tau_));
		} else {
		    acc <- Some(false);
		    return ((U,v,V,ltk,acc,k,ni_,nr,kmac,sid,2),
		            (zeron));
		}
	}


	oracle Run5(state: (Integer,Bool,Integer,Bits(n),Maybe(Bool),
	                    Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)),
						Maybe((Integer,Integer,Bits(n),Bits(n),Bits(n))),
						Integer),
	            tau:Bits(n)) -> ((Integer,Bool,Integer,Bits(n),Maybe(Bool),
				                    Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)),Maybe(Bits(n)),
				                    Maybe((Integer,Integer,Bits(n),Bits(n),Bits(n))),
				                    Integer),
				                   (Bool)) {
	    (U,u,V,ltk,acc,k,ni,nr,kmac,sid,mess) <- parse state;
        assert (acc  == None );              /* session is still running           */
        assert (mess == 2);
        assert (u    == false);              /* session belongs to initiator       */

        if (mac(Unwrap(kmac),zeron,4) == tau) {
		   return ((U,u,V,ltk,Some(true),k,ni,nr,kmac,sid,3),
		           (true));
		} else {
		    /* for false, the state is not updated so doesn't matter what we write as first argument */
		    return (state, (false));
		}
	}

}