WHITESPACE = _{ " " | "\r" | "\n" | "\t" }
COMMENT = _{ ";"+ ~ (!"\n" ~ ANY)* }

// Special Cases
defun = { "(" ~ "define-fun" ~ atom ~ list ~ ty ~ sexp ~ ")" }
sampleid = { "(" ~ "sample-id" ~ stringlit ~ stringlit ~ stringlit ~ ")" }

define_state_relation = { "(" ~ "define-state-relation" ~ atom ~ list ~ sexp ~ ")" }
define_lemma =          { "(" ~ "define-lemma" ~ atom ~ list ~ sexp ~ ")" }

// Fallback Entries
atom = @{ ( ASCII_ALPHA | "_" | "-" | "=" | "<" | ">" | "$" | "!" | "+" | "@" | "." | "*" ) ~ ( ASCII_ALPHA | ASCII_DIGIT | "_" | "-" | "=" | "<" | ">" | "$" | "!" | "+" | "@" | "." | "*" )* }
stringlit = _{ "\"" ~ string ~ "\"" }
string = { (!"\"" ~ ANY)* }

boolean = { "true" | "false" }
integer = @{"-"? ~ ASCII_DIGIT+ }
ty = { "Int" | "Bool" | sexp }
list = { "(" ~ sexp*~ ")" }

// Big Or, Fallback Entries last!
sexp = { sampleid | defun | define_state_relation | define_lemma | list | integer | boolean | stringlit | atom }
sexps = { SOI ~ sexp+ ~ EOI }


